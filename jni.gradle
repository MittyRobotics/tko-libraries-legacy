repositories {
    mavenCentral()
}

if (project.hasProperty('releaseMode')) {
    wpilibRepositories.addAllReleaseRepositories(project)
} else {
    wpilibRepositories.addAllDevelopmentRepositories(project)
}

// Apply C++ configuration
apply from: 'config.gradle'


// Set up exports properly
nativeUtils {
    exportsConfigs {
        // Main library is just default empty. This will export everything
        TkoLibrariesCpp {
        }
    }
    privateExportsConfigs {
        // Only export explicit symbols from driver library
        TkoLibrariesCppDriver {
            exportsFile = project.file("cpp-util-module/src/main/driver/symbols.txt")
        }
    }
}

model {
    components {
        TkoLibrariesCpp(NativeLibrarySpec) {
            sources {
                cpp {
                    source {
                        srcDirs 'cpp-util-module/src/main/native/cpp'
                        include '**/*.cpp'
                    }
                    exportedHeaders {
                        srcDirs 'cpp-util-module/src/main/native/include, cpp-util-module/src/main/native/cpp/drake'
                    }
                }
            }
            binaries.all {
                lib library: 'TkoLibrariesCppDriver', linkage: 'shared'
            }
            appendDebugPathToBinaries(binaries)
            nativeUtils.useRequiredLibrary(it, 'wpilib_shared')
        }

        TkoLibrariesCppDriver(JniNativeLibrarySpec) {
            enableCheckTask true
            javaCompileTasks << compileJava
//            jniCrossCompileOptions << JniCrossCompileOptions(nativeUtils.wpi.platforms.windowsx64)

            jniCrossCompileOptions << JniCrossCompileOptions(nativeUtils.wpi.platforms.roborio)
            // Leave these for future proofing
            jniCrossCompileOptions << JniCrossCompileOptions(nativeUtils.wpi.platforms.raspbian)
            jniCrossCompileOptions << JniCrossCompileOptions(nativeUtils.wpi.platforms.aarch64bionic)
            sources {
                cpp {
                    source {
                        srcDirs 'cpp-util-module/src/main/driver/cpp'
                        include '**/*.cpp'
                    }
                    exportedHeaders {
                        srcDirs 'cpp-util-module/src/main/driver/include'
                    }
                }
            }

            nativeUtils.useRequiredLibrary(it, "driver_shared")
        }
    }
}

apply from: 'publish.gradle'
